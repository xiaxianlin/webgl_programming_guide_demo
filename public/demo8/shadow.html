<!DOCTYPE html>
<html lang="en">
    <head>
        <meta charset="UTF-8" />
        <meta name="viewport" content="width=device-width, initial-scale=1.0" />
        <title>WebGL Demo</title>
        <style>
            canvas {
                border: 1px dashed #333;
            }
            .container {
                display: flex;
                justify-content: center;
            }
        </style>
    </head>
    <body>
        <div class="container">
            <canvas id="main" width="800" height="800"></canvas>
        </div>
        <script id="shadowVertexShader" type="x-shader/x-vertex">
            attribute vec4 a_Position;

            uniform mat4 u_MvpMatrix;

            void main(){
                gl_Position = u_MvpMatrix * a_Position;
            }
        </script>
        <script id="shadowFragmentShader" type="x-shader/x-fragment">
            precision mediump float;

            void main(){
                const vec4 bitShift = vec4(1.0, 256.0, 256.0 * 256.0, 256.0 * 256.0 * 256.0);
                const vec4 bitMask = vec4(1.0 / 256.0, 1.0 / 256.0, 1.0 / 256.0, 0.0);
                vec4 rgbaDepth = fract(gl_FragCoord.z * bitShift);
                rgbaDepth -= rgbaDepth.gbaa * bitMask;
                gl_FragColor = rgbaDepth;
            }
        </script>
        <script id="vertexShader" type="x-shader/x-vertex">
            attribute vec4 a_Position;
            attribute vec4 a_Color;

            uniform mat4 u_MvpMatrix;
            uniform mat4 u_MvpMatrixFromLight;

            varying vec4 v_PositionFromLight;
            varying vec4 v_Color;

            void main(){
                gl_Position = u_MvpMatrix * a_Position;
                v_PositionFromLight = u_MvpMatrixFromLight * a_Position;
                v_Color = a_Color;
            }
        </script>
        <script id="fragmentShader" type="x-shader/x-fragment">
            precision mediump float;

            uniform sampler2D u_Sampler;

            varying vec4 v_PositionFromLight;
            varying vec4 v_Color;

            float unpackDepth(const in vec4 rgbaDepth){
                const vec4 bitShift = vec4(1.0, 1.0/256.0, 1.0/(256.0 * 256.0), 1.0/(256.0 * 256.0 * 256.0));
                float depth = dot(rgbaDepth, bitShift);
                return depth;
            }

            void main(){
                vec3 shadowCoord = (v_PositionFromLight.xyz / v_PositionFromLight.w) / 2.0 + 0.5;
                vec4 rgbaDepth = texture2D(u_Sampler, shadowCoord.xy);
                float depth = unpackDepth(rgbaDepth);
                float visibility = (shadowCoord.z > depth + 0.0015) ? 0.7 : 1.0;
                gl_FragColor = vec4(v_Color.rgb * visibility, v_Color.a);
            }
        </script>
        <script src="../static/main.js"></script>
    </body>
</html>
